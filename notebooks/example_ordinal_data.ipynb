{
 "cells": [
  {
   "attachments": {},
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Overview\n",
    "\n",
    "In this example, we'll demonstrate the use of Matrix Linear Models (MLMs) with ordinal predictors, specifically when differences between sequential levels of the variable are of interest. To illustrate this, we'll create a simulated dataset in which the X matrix comprises a single ordinal variable, 'catvar'. This variable ranges from 1 to 5. This simple setup will provide a clear demonstration of how MLM can handle ordinal data.\n",
    "\n",
    "Just as a quick recap, our model formula is:\n",
    "\n",
    "$$Y = XBZ^T+E$$\n",
    "\n",
    "In this equation:\n",
    "\n",
    "- $Y_{n \\times m}$ is the response matrix\n",
    "- $X_{n \\times p}$ is the matrix for main predictors,\n",
    "- $Z_{m \\times q}$ denotes the response attributes matrix based on supervised knowledge,\n",
    "- $E_{n \\times m}$ is the error term, \n",
    "- $B_{p \\times q}$ is the matrix for main and interaction effects.\n",
    "\n",
    "This model formulation concisely summarizes the interactions among the elements of the matrix linear model framework.\n"
   ]
  },
  {
   "attachments": {},
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Data Generation\n",
    "\n",
    "Our dataset consists of a dataframe `X` with a single predictor. This predictor is ordinal data with `5 levels`, distributed over `n = 100` samples. Next, we define a response dataframe `Y` that consists of `m = 250` responses. \n",
    "To simulate the `Y` data, we need to construct the matrices `Z`, `B`, and `E`.\n",
    "The `Z` matrix imparts information about the response population, represented by the columns of `Y`, $y_{i \\in [1, 250]}$. This matrix has dimensions `250x4`.\n",
    "\n",
    "In this configuration, our coefficient matrix `B` is set to have dimensions `4x5`, matching the number of predictors in the design matrix `X` and the number of information categories in `Z`.\n",
    "\n",
    "Lastly, we define the noise matrix E to capture the error terms. This matrix is generated as a normally distributed matrix ($N (0, 1) $), introducing variability into our simulation."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "using MatrixLM, DataFrames, Random, Plots, StatsModels, Statistics\n",
    "Random.seed!(1)\n",
    "\n",
    "# Dimensions of matrices \n",
    "n = 100\n",
    "m = 250\n",
    "\n",
    "# Number of groupings designed in the Z matrix\n",
    "q = 4\n",
    "\n",
    "# Generate data with 1 ordinal categorical variable.\n",
    "dfX = DataFrame(catvar=rand(1:5, n));\n",
    "levels_catvar = sort(unique(dfX.catvar));"
   ]
  },
  {
   "attachments": {},
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "To derive the predictor design matrix, we employ the `design_matrix()` function and apply contrast coding using the `SeqDiffCoding()` system. This particular coding system is particularly useful for testing hypotheses related to \"sequential differences\" between the levels of our ordinal predictor."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "X_ctrst = Dict(\n",
    "             :catvar => SeqDiffCoding(levels = levels_catvar),\n",
    "           )\n",
    "           \n",
    "X = design_matrix(@mlmformula(1 + catvar), dfX, X_ctrst);\n",
    "p = size(X, 2);"
   ]
  },
  {
   "attachments": {},
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "The design matrix `X` has `p = 5` columns defined as:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "X_names = MatrixLM.design_matrix_names(@mlmformula(1 + catvar), dfX, X_ctrst)"
   ]
  },
  {
   "attachments": {},
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "We randomly generate a dataframe `Z` that provides information about whether a response $y_{i \\in [1, 250]}$, i.e., a column of `Y`, exhibits one of the four mutually exclusive attributes `{\"A\", \"B\", \"C\", \"D\"}`. To extract the design matrix of this \"column predictor\", we use the `design_matrix()` function and implement contrast coding with the `FullDummyCoding()` system.\n",
    "\n",
    "The `FullDummyCoding` system generates one indicator (1 or 0) column for each level, including the base level. This technique is sometimes called one-hot encoding and is widely used for categorical variables."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "dfZ = DataFrame(attribute= rand([\"A\", \"B\", \"C\", \"D\"], m))\n",
    "Z_ctrst = Dict(\n",
    "             :attribute => StatsModels.FullDummyCoding(),\n",
    "          )\n",
    "           \n",
    "Z = design_matrix(@mlmformula(0 + attribute), dfZ, Z_ctrst);"
   ]
  },
  {
   "attachments": {},
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "The design matrix `Z` has `q = 4` columns defined as:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "Z_names = MatrixLM.design_matrix_names(@mlmformula(0 + attribute), dfZ, Z_ctrst)"
   ]
  },
  {
   "attachments": {},
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "The error matrix `E` is obtained as follows:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "E = randn(n,m).*4;"
   ]
  },
  {
   "attachments": {},
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "We intentionally structure the coefficient matrix `B` according to a distinct pattern. By doing so, we enable a more straightforward visualization and interpretation of the results in the following steps:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "# (p,q)\n",
    "B = [\n",
    "    20.0  10.0 15.0 12.0;\n",
    "    0.01  7.0  0.05 0.01;\n",
    "    12.0  0.1  0.05 0.5;\n",
    "    0.01  12.0 0.05 0.03;\n",
    "    0.07  0.0  8.5  0.04;\n",
    "];"
   ]
  },
  {
   "attachments": {},
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Generate the response matrix `Y`:"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "Y = X*B*Z' + E;"
   ]
  },
  {
   "attachments": {},
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Now, construct the `RawData` object consisting of the response variable `Y` and row/column predictors `X` and `Z`. All three matrices must be passed in as 2-dimensional arrays. You have the option to specify if `X` and `Z` include an intercept (true) or not (false). If this information is not provided, the default is false (no intercept)."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "# Construct a RawData object\n",
    "dat = RawData(Response(Y), Predictors(X, Z, true, false));"
   ]
  },
  {
   "attachments": {},
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Model estimation"
   ]
  },
  {
   "attachments": {},
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Least-squares estimates for matrix linear models can be obtained by running `mlm`. An object of type `Mlm` will be returned, with variables for the coefficient estimates (`B`), the coefficient variance estimates (`varB`), and the estimated variance of the errors (`sigma`). By default, `mlm` estimates both row and column main effects (X and Z intercepts), but this behavior can be suppressed by setting `addXIntercept=false` and/or `addZIntercept=false`. Column weights for `Y` and the target type for variance shrinkage[^1] can be optionally supplied to `weights` and `targetType`, respectively. "
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "est = mlm(dat; addXIntercept=false, addZIntercept=false); # Model estimation"
   ]
  },
  {
   "attachments": {},
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## Model predictions and residuals"
   ]
  },
  {
   "attachments": {},
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "The coefficient estimates can be accessed using `coef(est)`. Predicted values and residuals can be obtained by calling `predict()` and `resid()`. By default, both of these functions use the same data used to fit the model. However, a new `Predictors` object can be passed into `predict()` as the `newPredictors` argument, and a new `RawData` object can be passed into `resid()` as the newData argument. For convenience, `fitted(est)` will return the fitted values by calling predict with the default arguments.  \n",
    "  \n",
    "To compare the estimated coefficients with the original matrix `B`, we will visualize the matrices using heatmaps. This graphical representation allows us to see the differences and similarities between the two readily."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "esti_coef = coef(est); # Get the coefficients of the model\n",
    "\n",
    "plot(\n",
    "    heatmap(B[end:-1:1, :], \n",
    "            size = (800, 300)),     \n",
    "    heatmap(esti_coef[end:-1:1, :], \n",
    "            size = (800, 300)),     \n",
    "    title = [\"\\$ \\\\mathbf{B}\\$\" \"\\$ \\\\mathbf{\\\\hat{B}}\\$\"]\n",
    ")\n"
   ]
  },
  {
   "attachments": {},
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Let's employ the same visualization method to compare the predicted values with the original `Y` response matrix. This allows us to gauge the accuracy of our model predictions."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "preds = predict(est); # Prediction value\n",
    "\n",
    "plot(\n",
    "    heatmap(Y[end:-1:1, :], \n",
    "            size = (800, 300)),     \n",
    "    heatmap(preds.Y[end:-1:1, :], \n",
    "            size = (800, 300), \n",
    "            # clims = (-2, 8)\n",
    "            ),     \n",
    "    title = [\"\\$ \\\\mathbf{Y}\\$\" \"\\$ \\\\mathbf{\\\\hat{Y}}\\$\"]\n",
    ")"
   ]
  },
  {
   "attachments": {},
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "The `resid()` function, available in `MatrixLM.jl`, computes residuals for each observation, helping you evaluate the discrepancy between the model's predictions and the observed data."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "resids = resid(est);\n",
    "\n",
    "plot(\n",
    "    heatmap(resids[end:-1:1, :], \n",
    "            size = (800, 300)),     \n",
    "    histogram(\n",
    "        (reshape(resids,250*100,1)),\n",
    "            grid  = false,\n",
    "            label = \"\",\n",
    "            size = (800, 300)),     \n",
    "    title = [\"Residuals\" \"Distribution of the residuals\"]\n",
    ")"
   ]
  },
  {
   "attachments": {},
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## T-statistics and permutation test"
   ]
  },
  {
   "attachments": {},
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "The t-statistics for an `Mlm` object (defined as `est.B ./ sqrt.(est.varB)`) can be obtained by running `t_stat`. By default, `t_stat` does not return the corresponding t-statistics for any main effects that were estimated by `mlm`, but they will be returned if `isMainEff=true`."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "tStats = t_stat(est);"
   ]
  },
  {
   "attachments": {},
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "Permutation p-values for the t-statistics can be computed by the `mlm_perms` function. `mlm_perms` calls the more general function `perm_pvals` and will run the permutations in parallel when possible. The illustrative example below runs only 5 permutations; a different number can be specified as the second argument. By default, the function used to permute `Y` is `shuffle_rows`, which shuffles the rows for `Y`. Alternative functions for permuting `Y`, such as `shuffle_cols`, can be passed into the argument `permFun`. `mlm_perms` calls `mlm` and `t_stat`, so the user is free to specify keyword arguments for `mlm` or `t_stat`; by default, `mlm_perms` will call both functions using their default behavior."
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "nPerms = 500\n",
    "tStats, pVals = mlm_perms(dat, nPerms, \n",
    "    addXIntercept=false, addZIntercept=false);\n",
    "\n",
    "plot(\n",
    "    heatmap(tStats[end:-1:1, :],\n",
    "            c = :bluesreds,\n",
    "            clims = (-400, 400),\n",
    "            xticks = (1:4, Z_names),\n",
    "            xrotation = 45,\n",
    "            yticks = (collect(5:-1:1), X_names),\n",
    "            bottom_margin = (10, :mm),\n",
    "            size = (800, 300)),  \n",
    "    heatmap(-log.(pVals[end:-1:1, :]),\n",
    "            grid = false,\n",
    "            xticks = (1:4, Z_names),\n",
    "            xrotation = 45,                            \n",
    "            yticks = (collect(5:-1:1), X_names),\n",
    "            bottom_margin = (10, :mm),\n",
    "            size = (800, 300)),       \n",
    "    title = [\"T Statistics\" \"- Log(P-values)\"]\n",
    ")"
   ]
  },
  {
   "attachments": {},
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "\n",
    "In this example, our interpretation of the results can be as follows: \n",
    "- the responses $y_{i \\in [1, 250]}$ that exhibit the \"A\" attribute in `Z` show significant differences between level 3 and level 2 of the predictor `catvar` in `X`.\n",
    "- the responses $y_{i \\in [1, 250]}$ that exhibit the \"B\" attribute in `Z` show significant differences between level 4 and level 3, as well as between level 2 and level 1 of the predictor `catvar` in `X`.\n",
    "- the responses $y_{i \\in [1, 250]}$ that exhibit the \"C\" attribute in `Z` show significant differences between level 5 and level 4 of the predictor `catvar` in `X`."
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "## References\n",
    "\n",
    "[^1]: Ledoit, O., & Wolf, M. (2003). Improved estimation of the covariance matrix of stock returns with an application to portfolio selection. Journal of empirical finance, 10(5), 603-621. "
   ]
  }
 ],
 "metadata": {
  "jupytext": {
   "formats": "ipynb,md"
  },
  "kernelspec": {
   "display_name": "Julia 1.12",
   "language": "julia",
   "name": "julia-1.12"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.12.2"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 4
}
